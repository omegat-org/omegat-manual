
plugins {
    id 'org.omegat.document-conventions'
    id 'org.omegat.publish-conventions'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version "2.0.0"
}

repositories {
    mavenCentral()
}

version = '6.1.0'

def manualLangs = project.property("manualLangs").split(',')
manualLangs.each { lang ->
    makeDocumentationTasks(lang)
}

def firstStepLangs = project.property("firstStepLangs").split(',')
firstStepLangs.each { lang ->
    makeFirstStepTask(lang)
}

tasks.register('updateManuals') {
    group = 'documentation'
    description = 'Update first pages and HTML manuals.'
    dependsOn manualHtmls, firstSteps
}

// Publish all the publications
publishing {
    repositories {
        maven {
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials(PasswordCredentials) {
                username = findProperty('ossrhUsername')
                password = findProperty('ossrhPassword')
            }
        }
    }
}

nexusPublishing.repositories {
    sonatype {
        nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
        snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        username.set(findProperty('ossrhUsername') as String)
        password.set(findProperty('ossrhPassword') as String)
    }
}

