import org.omegat.documentation.*

plugins {
    id 'org.omegat.documentation'
    id 'org.omegat.common-utilities'
    id 'signing'
}

def documentRootDir = 'src_docs'
def styleRootDir = file('src_docs/xsl')

tasks.register('genDocIndex', Copy) {
    def inputTemplate = layout.projectDirectory.file("${documentRootDir}/template/index.html")
    def docPropsFiles = fileTree(dir: "${documentRootDir}/manual", include: '**/version*.properties')
    def langInformation = docPropsFiles.collect { props ->
        def docVersion = loadProperties(props).version
        ['code'  : props.parentFile.name, 'nomanual': false, 'version': docVersion,
         'name'  : Locale.forLanguageTag(props.parentFile.name.replace('_', '-')).getDisplayName(),
         'status': docVersion == version ? 'up-to-date' : 'out-of-date']
    }
    description = 'Generate the docs index file'
    inputs.files docPropsFiles, inputTemplate
    from inputTemplate
    into layout.buildDirectory.dir("tmp/manual/")
    expand('languages': langInformation)
    doLast {
        copy {
            from layout.buildDirectory.file("tmp/manual/index.html")
            into layout.buildDirectory.dir("docs/manual")
        }
    }
    filteringCharset = 'UTF-8'
}

tasks.register('manualZips') {
    description = 'Build ZIP manuals to bundle into application. Requires container runtime.'
    group = 'documentation'
}

tasks.register('manualHtmls') {
    description = 'Build HTML manuals and zip for all languages. Requires container runtime.'
    finalizedBy genDocIndex
    group = 'documentation'
}

ext.makeDocumentationTasks = { lang ->

    def docbookInclude = tasks.register("docbookInclude${lang.capitalize()}", TransformationTask) {
        styleSheetFile.set(layout.projectDirectory.file("${styleRootDir}/passthrough.xsl"))
        outputFile.set(layout.buildDirectory.file("tmp/manual/${lang}/db5-all-included.xml"))
        inputFile.set(layout.projectDirectory.file("src_docs/manual/${lang}/OmegaTUsersManual_xinclude_full.xml"))
    }

    def copyJs = tasks.register("copyJs${lang.capitalize()}", Copy) {
        description 'Copy CSS and JS files to tmp'
        into(layout.buildDirectory.dir("tmp/manual/${lang}/js/"))
        from fileTree(dir: "${documentRootDir}/docbook/js")
    }

    def copyCss = tasks.register("copyCss${lang.capitalize()}", Copy) {
        description 'Copy CSS and JS files to tmp'
        into(layout.buildDirectory.dir("tmp/manual/${lang}/css/"))
        from fileTree(dir: "${documentRootDir}/docbook/css")
    }

    def copyImages = tasks.register("copyImages${lang.capitalize()}", Copy) {
        description 'Copy Images and CSS file to tmp'
        into layout.buildDirectory.dir("tmp/manual/${lang}/images/")
        from fileTree(dir: layout.projectDirectory.dir("src_docs/manual/${lang}/images/"), include: "*.png")
        from layout.projectDirectory.file("images/OmegaT.svg")
    }

    def copyCssJsTarget = tasks.register("copyCssJsTarget${lang.capitalize()}", Copy) {
        description 'Copy CSS and JS files to target'
        into(layout.buildDirectory.dir("docs/manual/${lang}/"))
        from fileTree(dir: "${documentRootDir}/docbook", includes: ['css/omegat.css', 'js/*'])
    }

    def copyImagesTarget = tasks.register("copyImagesTarget${lang.capitalize()}", Copy) {
        description 'Copy Images and CSS file to target'
        into layout.buildDirectory.dir("docs/manual/${lang}/images/")
        from fileTree(dir: layout.projectDirectory.dir("src_docs/manual/${lang}/images/"), include: "*.png")
        from layout.projectDirectory.file("images/OmegaT.svg")
    }

    def docbookHtml = tasks.register("docbookHtml${lang.capitalize()}", DocbookHtmlTask) {
        description = 'Generate chunked HTML documentation'
        styleSheetFile.set(layout.projectDirectory.file("${styleRootDir}/db5-xhtml.xsl"))
        inputFile.set(layout.projectDirectory.file("${documentRootDir}/manual/${lang}/OmegaTUsersManual_xinclude_full.xml"))
        outputFile.set(layout.buildDirectory.file("tmp/manual/${lang}/index.html"))
        dependsOn(copyImages, copyCss, copyJs)
    }

    def whcToc = tasks.register("whcToc${lang.capitalize()}", TransformationTask) {
        description = 'Generates a whc header and index.'
        styleSheetFile.set(layout.projectDirectory.file("${styleRootDir}/db5-whc-toc.xsl"))
        inputFile.set(layout.buildDirectory.file("tmp/manual/${lang}/db5-all-included.xml"))
        outputFile.set(layout.buildDirectory.file("tmp/manual/${lang}/_whc-toc.xml"))
        dependsOn(docbookInclude)
    }

    def whcIndex = tasks.register("whcIndex${lang.capitalize()}", TransformationTask) {
        description = 'Generates a whc header and index.'
        styleSheetFile.set(layout.projectDirectory.file("${styleRootDir}/db5-whc-index.xsl"))
        inputFile.set(layout.buildDirectory.file("tmp/manual/${lang}/db5-all-included.xml"))
        outputFile.set(layout.buildDirectory.file("tmp/manual/${lang}/_whc-index.xml"))
        dependsOn(docbookHtml, whcToc)
    }

    def whcHeader = tasks.register("whcHeader${lang.capitalize()}", TransformationTask) {
        description = 'Generates a whc header and index.'
        styleSheetFile.set(layout.projectDirectory.file("${styleRootDir}/whc-header.xsl"))
        inputFile.set(layout.buildDirectory.file("tmp/manual/${lang}/db5-all-included.xml"))
        outputFile.set(layout.buildDirectory.file("tmp/manual/${lang}/_whc-header.xml"))
        dependsOn(docbookInclude)
    }

    def buildDocumentTask = tasks.register("manual${lang.capitalize()}", WhcTask) {
        description = 'Builds the whc contents.'
        inputFile.set(layout.buildDirectory.file("tmp/manual/${lang}/_whc-index.xml"))
        contentFiles.set(fileTree(dir: layout.buildDirectory.dir("tmp/manual/${lang}"), include: '*.html', exclude: '_*'))
        tocFile.set(layout.buildDirectory.file("tmp/manual/${lang}/_whc-toc.xml"))
        headerFile.set(layout.buildDirectory.file("tmp/manual/${lang}/_whc-header.xml"))
        outputDirectory.set(layout.buildDirectory.dir("docs/manual/${lang}"))
        documentLayout.set('simple')
        localJQuery.set(true)
        parameterList.set([
                '--navigation-background-color', '#FDFDFD',
                '--field-background-color', '#FDFDFD',
                '--panel-background-color', '#FDFDFD',
        ])
        dependsOn(whcHeader, whcToc, whcIndex, copyImagesTarget, copyCssJsTarget)
        group('documentation')
    }
    assemble.dependsOn buildDocumentTask
    manualHtmls.dependsOn buildDocumentTask

    def zipTask = tasks.register("manualZip${lang.capitalize()}", Zip) {
        from fileTree(dir: layout.buildDirectory.file("docs/manual/${lang}"))
        from fileTree(dir: "${documentRootDir}/manual/${lang}", include: '**/version*.properties')
        archiveFileName = "${lang}.zip"
        destinationDirectory = file(layout.buildDirectory.dir("docs/manuals/"))
        dependsOn docbookHtml
    }
    assemble.dependsOn zipTask
    manualZips.dependsOn zipTask
}

tasks.register('firstSteps') {
    description = 'Build first pages for all languages at docs/greetings/. Requires Docker.'
    group = 'documentation'
}
assemble.dependsOn firstSteps

ext.makeFirstStepTask = { lang ->

    def firstStepsHtmlFinally = tasks.register("firstSteps${lang.capitalize()}Finally", Copy) {
        description 'Copy CSS files to target'
        into(layout.buildDirectory.dir("docs/greetings/${lang}/"))
        from fileTree(dir: "${documentRootDir}/docbook", includes: ['css/*'])
    }

    def firstStepsHtml = tasks.register("firstSteps${lang.capitalize()}", DocbookHtmlTask) {
        description 'Generate chunked HTML documentation'
        inputFile.set(file("${documentRootDir}/greeting/${lang}/First_Steps.xml"))
        outputFile.set(layout.buildDirectory.file("docs/greetings/${lang}/first_steps.html"))
        styleSheetFile.set(layout.projectDirectory.file("${styleRootDir}/html.xsl"))
        finalizedBy firstStepsHtmlFinally
    }
    firstSteps.dependsOn firstStepsHtml
}
