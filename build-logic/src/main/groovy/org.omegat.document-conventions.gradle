import org.omegat.documentation.*

plugins {
    id 'org.omegat.documentation'
    id 'org.omegat.common-utilities'
    id 'signing'
}

def documentRootDir = 'src_docs'
def styleRootDir = 'src_docs/xsl'

tasks.register('genDocIndex', Copy) {
    def inputTemplate = layout.projectDirectory.file("${documentRootDir}/template/index.html")
    def docPropsFiles = fileTree(dir: "${documentRootDir}/manual", include: '**/version*.properties')
    def langInformation = docPropsFiles.collect { props ->
        def docVersion = loadProperties(props).version
        ['code'  : props.parentFile.name, 'nomanual': false, 'version': docVersion,
         'name'  : Locale.forLanguageTag(props.parentFile.name.replace('_', '-')).getDisplayName(),
         'status': docVersion == version ? 'up-to-date' : 'out-of-date']
    }
    description = 'Generate the docs index file'
    inputs.files docPropsFiles, inputTemplate
    from inputTemplate
    into layout.buildDirectory.dir("tmp/manual/")
    expand('languages': langInformation)
    doLast {
        copy {
            from layout.buildDirectory.file("tmp/manual/index.html")
            into layout.buildDirectory.dir("docs/manual")
        }
    }
    filteringCharset = 'UTF-8'
}

tasks.register('manualZips') {
    description = 'Build ZIP manuals to bundle into application. Requires container runtime.'
    group = 'documentation'
}

tasks.register('manualHtmls') {
    description = 'Build HTML manuals and zip for all languages. Requires container runtime.'
    finalizedBy genDocIndex
    group = 'documentation'
}

ext.makeDocumentationTasks = { lang ->

    def docbookInclude = tasks.register("docbookInclude${lang.capitalize()}", TransformationTask) {
        styleSheetFile.set(layout.projectDirectory.file("${styleRootDir}/passthrough.xsl"))
        outputFile.set(layout.buildDirectory.file("tmp/manual/${lang}/_whc/db5-all-included.xml"))
        inputFile.set(layout.projectDirectory.file("${documentRootDir}/manual/${lang}/OmegaTUsersManual_xinclude_full.xml"))
        contentFiles.set(fileTree(dir: layout.projectDirectory.dir("${documentRootDir}/manual/${lang}"), include: '*.xml'))
    }

    def copyJs = tasks.register("copyJs${lang.capitalize()}", Copy) {
        description 'Copy CSS and JS files to tmp'
        into(layout.buildDirectory.dir("tmp/manual/${lang}/js/"))
        from fileTree(dir: "${documentRootDir}/docbook/js")
    }

    def copyCss = tasks.register("copyCss${lang.capitalize()}", Copy) {
        description 'Copy CSS and JS files to tmp'
        into(layout.buildDirectory.dir("tmp/manual/${lang}/css/"))
        from fileTree(dir: "${documentRootDir}/docbook/css")
        from file("${documentRootDir}/style/omegat.css")
    }

    def copyImages = tasks.register("copyImages${lang.capitalize()}", Copy) {
        description 'Copy Images and CSS file to tmp'
        into layout.buildDirectory.dir("tmp/manual/${lang}/images/")
        from fileTree(dir: layout.projectDirectory.dir("${documentRootDir}/manual/${lang}/images/"), include: "*.png")
        from layout.projectDirectory.file("images/OmegaT.svg")
    }

    def copyCssTarget = tasks.register("copyCssTarget${lang.capitalize()}", Copy) {
        description 'Copy CSS and JS files to target'
        from layout.projectDirectory.file("src_docs/style/omegat.css")
        into(layout.buildDirectory.dir("docs/manual/${lang}/css/"))
    }

    def copyJsTarget = tasks.register("copyJsTarget${lang.capitalize()}", Copy) {
        description 'Copy CSS and JS files to target'
        from fileTree(dir: "${documentRootDir}/docbook/js", includes: ['*.js'])
        into(layout.buildDirectory.dir("docs/manual/${lang}/js/"))
    }

    def copyImagesTarget = tasks.register("copyImagesTarget${lang.capitalize()}", Copy) {
        description 'Copy Images and CSS file to target'
        into layout.buildDirectory.dir("docs/manual/${lang}/images/")
        from fileTree(dir: layout.projectDirectory.dir("${documentRootDir}/manual/${lang}/images/"), include: "*.png")
        from layout.projectDirectory.file("images/OmegaT.svg")
    }

    def docbookHtml = tasks.register("docbookHtml${lang.capitalize()}", DocbookHtmlTask) {
        description = 'Generate chunked HTML documentation'
        styleSheetFile.set(layout.projectDirectory.file("${styleRootDir}/db5-xhtml.xsl"))
        inputFile.set(layout.projectDirectory.file("${documentRootDir}/manual/${lang}/OmegaTUsersManual_xinclude_full.xml"))
        outputFile.set(layout.buildDirectory.file("tmp/manual/${lang}/index.html"))
        contentFiles.set(fileTree(dir: layout.projectDirectory.dir("${documentRootDir}/manual/${lang}"), include: '*.xml'))
        css.set("css/omegat.css")
        outputs.cacheIf { false }
        // debug.set("chunk-cleanup")
        dependsOn(copyImages, copyCss, copyJs)
    }

    def whcToc = tasks.register("whcToc${lang.capitalize()}", TransformationTask) {
        description = 'Generates a whc header and index.'
        styleSheetFile.set(layout.projectDirectory.file("${styleRootDir}/db5-whc-toc.xsl"))
        inputFile.set(layout.buildDirectory.file("tmp/manual/${lang}/_whc/db5-all-included.xml"))
        outputFile.set(layout.buildDirectory.file("tmp/manual/${lang}/_whc/_whc-toc.xml"))
        dependsOn(docbookHtml, docbookInclude)
    }

    def whcIndex = tasks.register("whcIndex${lang.capitalize()}", TransformationTask) {
        description = 'Generates a whc header and index.'
        styleSheetFile.set(layout.projectDirectory.file("${styleRootDir}/db5-whc-index.xsl"))
        inputFile.set(layout.buildDirectory.file("tmp/manual/${lang}/_whc/db5-all-included.xml"))
        outputFile.set(layout.buildDirectory.file("tmp/manual/${lang}/_whc/_whc-index.xml"))
        dependsOn(docbookHtml, whcToc)
    }

    def whcHeader = tasks.register("whcHeader${lang.capitalize()}", TransformationTask) {
        description = 'Generates a whc header and index.'
        styleSheetFile.set(layout.projectDirectory.file("${styleRootDir}/whc-header.xsl"))
        inputFile.set(layout.buildDirectory.file("tmp/manual/${lang}/_whc/db5-all-included.xml"))
        outputFile.set(layout.buildDirectory.file("tmp/manual/${lang}/_whc/_whc-header.xml"))
        dependsOn(docbookHtml, docbookInclude)
    }

    def buildDocumentTask = tasks.register("manual${lang.capitalize()}", WhcTask) {
        description = 'Builds the whc contents.'
        inputFile.set(layout.buildDirectory.file("tmp/manual/${lang}/_whc/_whc-index.xml"))
        contentFiles.set(fileTree(dir: layout.buildDirectory.dir("tmp/manual/${lang}"), include: '*.html', exclude: '_*'))
        tocFile.set(layout.buildDirectory.file("tmp/manual/${lang}/_whc/_whc-toc.xml"))
        headerFile.set(layout.buildDirectory.file("tmp/manual/${lang}/_whc/_whc-header.xml"))
        outputDirectory.set(layout.buildDirectory.dir("docs/manual/${lang}"))
        documentLayout.set('simple')
        localJQuery.set(true)
        parameterList.set([
                '--navigation-background-color', '#FDFDFD',
                '--field-background-color', '#FDFDFD',
                '--panel-background-color', '#FDFDFD',
        ])
        outputs.cacheIf { false }
        dependsOn(whcHeader, whcToc, whcIndex, copyImagesTarget, copyCssTarget, copyJsTarget)
        group('documentation')
    }
    assemble.dependsOn buildDocumentTask
    manualHtmls.dependsOn buildDocumentTask

    def zipTask = tasks.register("manualZip${lang.capitalize()}", Zip) {
        from fileTree(dir: layout.buildDirectory.file("docs/manual/${lang}"))
        from fileTree(dir: "${documentRootDir}/manual/${lang}", include: '**/version*.properties')
        archiveFileName = "${lang}.zip"
        destinationDirectory = file(layout.buildDirectory.dir("docs/manuals/"))
        dependsOn docbookHtml
    }
    assemble.dependsOn zipTask
    manualZips.dependsOn zipTask
}

def sourceDir = file("src_docs/manual/4")
def targetDir = file("src_docs/manual/en")
def xslFile = file("src_docs/xsl/db4-upgrade.xsl")

def upgradeTasks = fileTree(sourceDir).include("**/*.xml").collect { sourceFile ->
    def relativePath = sourceDir.toPath().relativize(sourceFile.toPath())
    def targetFile = targetDir.toPath().resolve(relativePath).toFile()
    def taskName = "upgradeManual_${sourceFile.name.replaceAll(/[^a-zA-Z0-9]/, '_')}"

    return tasks.register(taskName, TransformationTask) {
        styleSheetFile.set(xslFile)
        inputFile.set(sourceFile)
        outputFile.set(targetFile)
        xIncludeAware.set(false)
    }
}

// Register the aggregate task and link it to the individual ones
tasks.register('upgradeAllManualsEn') {
    group = 'documentation'
    description = 'Upgrades all DocBook 4 XML files to DocBook 5 for English locale.'
    dependsOn upgradeTasks
}

tasks.register('firstSteps') {
    description = 'Build first pages for all languages at docs/greetings/. Requires Docker.'
    group = 'documentation'
}
assemble.dependsOn firstSteps

ext.makeFirstStepTask = { lang ->

    def firstStepsHtmlFinally = tasks.register("firstSteps${lang.capitalize()}Finally", Copy) {
        description 'Copy CSS files to target'
        into(layout.buildDirectory.dir("docs/greetings/${lang}/"))
        from layout.projectDirectory.file("src_docs/style/greeting.css")
    }

    def firstStepsHtml = tasks.register("firstSteps${lang.capitalize()}", DocbookHtmlTask) {
        description 'Generate chunked HTML documentation'
        inputFile.set(file("${documentRootDir}/greeting/${lang}/First_Steps.xml"))
        outputFile.set(layout.buildDirectory.file("docs/greetings/${lang}/first_steps.html"))
        styleSheetFile.set(layout.projectDirectory.file("${styleRootDir}/html.xsl"))
        css.set("greeting.css")
        finalizedBy firstStepsHtmlFinally
    }
    def philosophyHtml = tasks.register("philosophy${lang.capitalize()}", DocbookHtmlTask) {
        description = 'Generates a philosophy HTML'
        inputFile.set(file("${documentRootDir}/greeting/${lang}/Philosophy.xml"))
        outputFile.set(layout.buildDirectory.file("docs/greetings/${lang}/philosophy.html"))
        styleSheetFile.set(layout.projectDirectory.file("${styleRootDir}/html.xsl"))
        css.set("greeting.css")
    }
    firstSteps.dependsOn firstStepsHtml
    firstSteps.dependsOn philosophyHtml
    firstSteps.dependsOn firstStepsHtmlFinally
}
