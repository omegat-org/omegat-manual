plugins {
    id 'groovy'
    id 'groovy-gradle-plugin'
    id 'maven-publish'
}

gradlePlugin {
    plugins {
        documentationPlugin {
            id = 'org.omegat.documentation'
            implementationClass = 'org.omegat.documentation.DocumentationPlugin'
        }
    }
}

repositories {
    mavenCentral()
    // 'repository' for getting docbook xsd
    ivy {
        url = 'https://docbook.org/xml'
        patternLayout {
            artifact '[revision]/[module]-[revision].[ext]'
        }
        content {
            includeGroup 'docbook'
        }
        metadataSources {
            artifact()
        }
    }
    gradlePluginPortal()
}

dependencies {
    compileOnly gradleApi()
    compileOnly localGroovy()

    // 3rd party gradle plugin
    implementation 'org.hidetake.ssh:org.hidetake.ssh.gradle.plugin:2.11.2'

    // dependencies on Maven
    implementation 'xerces:xercesImpl:2.12.2'
    implementation 'org.xmlresolver:xmlresolver:5.2.2'
    implementation 'tokyo.northside:whc:3.6.0'

    // XSLT processor
    implementation 'net.sf.saxon:Saxon-HE:12.5'

    // DocBook 5 XSL stylesheet and processor
    implementation 'org.docbook:docbook-xslTNG:2.5.0'

    // dependencies not on Maven
    // https://docbook.org/xml/5.0.1/docbook-5.0.1.zip
    implementation 'docbook:docbook-xml:4.5:resources@zip'
    implementation 'docbook:docbook:5.0.1:resources@zip'
}
